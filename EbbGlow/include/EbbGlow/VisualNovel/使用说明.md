# 使用说明

## 符号解释

`[]`  
由 `[]` 所包裹的特性，为不推荐使用的特性。它确实存在于当前版本，但后续有可能更改/删除（也有可能永不删除），但您不应该过度使用和依赖此特性。

## 名词解释

### 1. 函数  
函数是预定义的最小执行单元，一个函数包含函数名和参数列表（由 `()` 包裹，由 `,` 分隔参数）。函数将从函数名开始被读取解析，直到 `)`；当到达 `)` 时，`[会丢弃所有字符直到换行符，并丢弃换行符]`。

### 2. 段  
段由一个段类型标识符开始，如 `Scene` / `Global`。每个段是一个执行块，会顺序执行段内所有函数，并在执行完毕后依据段类型和具体行为跳转至其余段执行。

### 3. 变量  
变量是在 `Global` 段中定义的、拥有名字且可以保存值的标识符，变量使用 `$varName` ‘来进行引用：  
- 对于期待**值**的上下文，将返回变量的值；  
- 对于期待**变量引用**的上下文（如赋值函数的第一个参数），将返回变量引用，以供目标函数修改此变量。

---

## Global 段

`Global` 段依靠函数指定脚本的初始化参数，如包含哪些文件、定义哪些变量、初始场景是哪一个。这些函数可以且仅可以在 `Global` 段使用。  


- `String(varName)`  
  定义一个字符串变量。  

- `String(varName, N)`  
  定义一个长度为 `N`（正整数）的字符串数组。可通过 `$varName[0]` 至 `$varName[N-1]` 访问；若省略下标（如 `$varName`），则默认访问 `$varName[0]`。

- `Number(varName)`  
  定义一个数字变量（内部为 `double`；在需要整数的上下文中使用 `(int)round()` 转换）。

- `Number(varName, N)`  
  定义一个长度为 `N` 的数字数组，访问方式同字符串数组。

- `Include(String path)`  
  将指定路径的脚本文件附加到当前脚本末尾一并解析。  
  示例：  
  `Include("Sc/Script.dat")` 
  千万千万不要循环包含，会出问题的(／‵Д′)／~ ╧╧
- `Begin(String sceneName)`  
  指定脚本执行的第一个 `Scene` 名称。  
  示例：  
  `Begin("sc_0_0")`

---

## Scene 段

每一个 `Scene` 段对应一个画面场景，依赖若干函数描述画面内容。每一个场景拥有**类型**和**名称**：类型规定场景的行为，名称则用于跳转标识。

示例：  
`Scene sc_0_1(TextScene : sc_0_2)`  
表明这是一个 `Scene` 段，名称为 `sc_0_1`，类型为 `TextScene`，向 `TextScene` 传递的参数为 `sc_0_2`。

场景参数均被视作字符串，且**无需用 `""` 包裹**；但具体执行时会动态转换类型。例如，当某参数期待整数时，传递 `0`、`123456` 是可行的；传递 `a`、`qwerty` 则可能导致运行时崩溃。

### 支持的 Scene 类型

#### TextScene  
`Scene name(TextScene : targetScene)`  
接受一个参数（字符串），表示跳转目标场景名。  
在接收到以下任一输入时跳转：  
- 鼠标左键单击  
- 空格键按下  
- Ctrl 键按下  
- 鼠标滚轮向下滚动  
- 触摸屏单击  

示例：  
`Scene sc_0_1(TextScene : sc_0_2)`

#### SelectScene  
`Scene name(SelectScene : target1, target2, ...)`  
接受可变数量参数（字符串），依次对应各按钮的跳转目标。  
应调用一个或多个 `Button` 函数创建按钮；按钮按下时，按创建顺序跳转到参数列表中的对应场景。  

示例：  
```
Scene sc_0_2(SelectScene : sc_1_2, sc_1_4)

Button(0.5, 0.6, 3.6585366, 0.15625, "场景2-语言1", "场景2-语言2", "场景2-语言3", "场景2-语言4", @Center, 255, 255, 255, 255, "resource\\img\\Button0.png", "resource\\img\\Button1.png", "resource\\img\\Button2.png")

Button(0.5, 0.6, 3.6585366, 0.15625, "场景4-语言1", "场景4-语言2", "场景4-语言3", "场景4-语言4", @Center, 255, 255, 255, 255, "resource\\img\\Button0.png", "resource\\img\\Button1.png", "resource\\img\\Button2.png")
```
第一个按钮跳转至 `sc_1_2`，第二个跳转至 `sc_1_4`。  
`[Button 调用次数应 ≤ 参数数量，否则会造成运行时崩溃]`

#### DelayScene  
`Scene name(DelayScene : targetScene, delayMs)`  
- `targetScene`：字符串，目标场景名；  
- `delayMs`：数字，延迟毫秒数。  

示例：  
`Scene sc_0_3(DelayScene : sc_1_2, 500)`  
表示启动后 500 毫秒跳转至 `sc_1_2`(按帧对齐，不少于500ms)。

---

## Scene 段内可用函数

### TextScene  
`TextScene(String lan0, String lan1, String lan2, String lan3, String bgPath, Tag drawState)`  
- `lan0`–`lan3`：四语言文本；  
- `bgPath`：背景图片路径；  
- `drawState`：绘制方式，`@Cover` 或 `@Contain`；  
  - `@Cover`：拉伸填满全屏，超出部分裁剪；  
  - `@Contain`：等比缩放至边缘，未充满区域为透明。  
  `[当在 TextScene 类型场景中调用时，若文本未播完，首次点击将立即标记为“播放完毕”，后续点击才触发跳转]`

### Chr  
`Chr(String name)`  
显示角色名于文本框左上角（使用 `cfg.chrNameBackGround` 及偏移配置）。  
`[将会在文本框左上显示该角色名称]`

### Button  
`Button(Number X, Number Y, Number ratio, Number width, String lan0, String lan1, String lan2, String lan3, Tag alignment, Number R, Number G, Number B, Number A, String iconNormal, String iconHover, String iconPress)`  

- `X`, `Y`：位置（屏幕宽/高百分比）；  
- `ratio`：宽高比（用于检测区域）；  
- `width`：按钮宽度（占屏宽百分比）；  
- `lan0`–`lan3`：四语言文本；  
- `alignment`：`@Center` 或 `@Normal`；`[任何非 @Center 值均视为 @Normal]`  
- `R/G/B/A`：文本颜色（0–255）；  
- `iconNormal`/`iconHover`/`iconPress`：三状态图标路径。

### Image  
`Image(Number X, Number Y, Number ratio, Number width, String imgPath, Tag drawState, Tag alignment, Number layerDepth)`  
- `X`, `Y`：绘制位置（屏幕宽/高百分比）；  
- `ratio`, `width`：定义绘制区域尺寸；  
- `imgPath`：图片路径；  
- `drawState`：`@Cover` 或 `@Contain`；`[任何非 @Cover 值均视为 @Contain]`  
- `alignment`：`@Center` 或 `@Normal`；`[任何非 @Center 值均视为 @Normal]`  
- `layerDepth`：图层深度（0–15，越大越靠前）。

### SetStr  
`SetStr(VariableRef varRef, String value)`  
将 `value` 赋值给变量引用 `varRef` 所指向的变量。

预定义变量（**可写不可读**）：  
- `$TARGET_LIST[i]`：修改当前场景第 `i` 个跳转参数。  
示例：  
```
Scene sc_0_1(TextScene : sc_0_2)
SetStr($TARGET_LIST[0], sc_0_3)
```
将跳转目标由 `sc_0_2` 改为 `sc_0_3`。  
`[所有参数修改在函数执行完毕后统一生效，执行过程中暂存]`

### SetNum  
`SetNum(VariableRef varRef, Number value)`  
数字类型赋值。  
`[暂无预定义数字变量]`

### if  
`if (Number expr) { ... }`  
- 仅支持数字比较：`>`, `<`, `>=`, `<=`, `==`, `!=`；  
- 支持四则运算（`+ - * / %`）、括号、变量；  
- `[不支持逻辑运算（&& || !）、链式比较（如 1 <= $var < 10）、字符串比较]`  

示例：  
```
Scene sc_0_1(TextScene : sc_0_2)
if ($affection >= 100) { SetStr($TARGET_LIST[0], sc_0_3) }
if ($affection < 100) {
    SetStr($TARGET_LIST[0], sc_1_0)
}
```

### TextBox  
`TextBox(Number X, Number Y, Number textRelativeSize, Number R, Number G, Number B, Number A, String fontPath, Number layerDepth, Tag alignment)`  
- `X`, `Y`：位置（屏幕宽/高百分比）；  
- `textRelativeSize`：相对文本大小（会按 `min(1920,1080)` 缩放）；  
- `R/G/B/A`：颜色（0–255）；  
- `fontPath`：字体路径（空或无效则用默认字体）；  
- `layerDepth`：图层深度；  
- `alignment`：`@Center` 或 `@Default`。

### KeyFrameAnimation  
`KeyFrameAnimation(String imgPath, Number layerDepth, Tag loop)`  
- `imgPath`：动画图片路径；  
- `layerDepth`：图层深度；  
- `loop`：`@Loop` 或 `@NotLoop`；`[任何非 @Loop 值均视为 @NotLoop]`

### AddKeyFrame  
`AddKeyFrame(Number X, Number Y, Number originX, Number originY, Number scale, Number rotation, Number duration, Number alpha, Tag alignment)`  
- `X`, `Y`：本帧末位置（屏幕宽/高百分比）；  
- `originX`, `originY`：旋转中心（相对于图片左上角的比例）；  
- `scale`：缩放系数；  
- `rotation`：本帧末旋转角度（度）；  
- `duration`：本帧持续时间（秒）；  
- `alpha`：透明度（0–255）；  
- `alignment`：`@Center` 或 `@Default`。  

规则：  
- 第一帧：静止 `duration` 秒；  
- 后续帧：从上一帧线性插值至本帧；  
- 循环时：最后一帧结束后**直接跳回第一帧**（无插值）；  
- `AddKeyFrame` 必须紧随 `KeyFrameAnimation` 调用，中间不得插入其他函数。

示例：  
```
KeyFrameAnimation("resource\\img\\backGround.png", 6, @Loop)
AddKeyFrame(0.5, 0.5, 0, 0, 0.5, 0, 0, 255, @Center)
AddKeyFrame(0.25, 0.75, 0, 0, 0.5, 0, 0.5, 255, @Center)
AddKeyFrame(0.5, 0.5, 0, 0, 0.5, 0, 0.5, 255, @Center)
```

### SetBGM  
`SetBGM(String path, Number volumeIndex)`  
- `path`：背景音乐路径；  
- `volumeIndex`：音量索引（对应 `cfg.volumes` 数组下标）；  
- **循环播放**。

### SetVoice  
`SetVoice(String path, Number volumeIndex)`  
- `path`：语音文件路径；  
- `volumeIndex`：音量索引；  
- **不循环播放**。

### 注释  
`//` 表示注释，**当且仅当位于行首时生效**。

---

### 示例

```
Global
Begin("scMenu")
String str


Scene scMenu(SelectScene : load_0_1, sc_0_1)
Button(1 / 2, 1 / 2,
	   3.6585366, 0.15625,
	   "场景1-语言1 ",
	   "场景1-语言2",
	   "场景1-语言3",
	   "场景1-语言4",
	   @Center,
	   255, 255, 255, 255,
	   "resource\\img\\Button0.png", "resource\\img\\Button1.png", "resource\\img\\Button2.png")
Button(0.5, 0.6, 3.6585366, 0.15625, "场景2-语言1 ", "场景2-语言2", "场景2-语言3", "场景2-语言4", @Center, 255, 255, 255, 255, "resource\\img\\Button0.png", "resource\\img\\Button1.png", "resource\\img\\Button2.png")
Image(0.5, 0.5, 16 / 9, 0.5, "resource\\img\\backGround.png", @Contain, @Center, 1)
SetStr($str, "A:\\CloudMusic\\UnicornPhantom - 漫步.mp3")


Scene load_0_1(DelayScene : scMenu, 0)
//Image(0, 0, 16 / 9, 1, "resource\\img\\backGround.png", @Contain, @Default, 1)
//TextBox(0.5, 0.5, 120, "Loading...", 255, 255, 255, 255, "", 2, @Center)


Scene sc_0_1(TextScene : sc_0_2)
TextScene("测试文本",
		  "測試文本",
		  "テストテキスト",
		  "Test text.",
		  "resource\\img\\backGround.png", @Cover)

SetBGM($str, 0)


KeyFrameAnimation("resource\\img\\backGround.png", 6, @Loop)
AddKeyFrame(0.5, 0.5, 0, 0,
			0.5, 0, 0, 255, @Center)
AddKeyFrame(0.25, 0.75, 0, 0,
			0.5, 0, 0.5, 255, @Center)
AddKeyFrame(0.5, 0.5, 0, 0,
			0.5, 0, 0.5, 255, @Center)


Scene sc_0_2(TextScene : sc_0_3)
TextScene("\"这个脚本加载器似乎能跑起来？\"",
		  "測試文本",
		  "テストテキスト",
		  "Test text.",
		  "resource\\img\\backGround.png", @Cover)
Chr("奇怪の作者")


Scene sc_0_3(TextScene : sc_0_4)
TextScene("长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长长的测试文本", "语言1", "语言2", "语言3", "", @Cover)


Scene sc_0_4(TextScene : scMenu)
TextScene("回去循环吧", "语言1", "语言2", "语言3", "resource\\img\\backGround.png", @Cover)
```

